apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs"
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'ru.cleverpumpkin.proguard-dictionaries-generator'


proguardDictionaries {
    dictionaryNames = [
            "build/class-dictionary",
            "build/package-dictionary",
            "build/obfuscation-dictionary"
    ]
}

def signingProperties = new Properties()
file("../signing.properties").withInputStream { signingProperties.load(it) }

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}


android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "net.onefivefour.android.bitpot"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 7
        versionName "1.0.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument 'androidx.benchmark.suppressErrors', 'DEBUGGABLE,UNLOCKED,UNSUSTAINED-ACTIVITY-MISSING'

        // Make sure this is consistent with the redirect URI used in res/raw/auth_config.json,
        // or specify additional redirect URIs in AndroidManifest.xml
        manifestPlaceholders = [
                'appAuthRedirectScheme': 'net.onefivefour.android.bitpot'
        ]

        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "COMMIT_HASH", "\"${getGitHash()}\""
    }

    signingConfigs {
        release {
            storeFile file(signingProperties.getProperty("signing.keystorePath"))
            storePassword signingProperties.getProperty("signing.keystorePassword")
            keyAlias signingProperties.getProperty("signing.keyAlias")
            keyPassword signingProperties.getProperty("signing.keyPassword")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            minifyEnabled false
            debuggable true
        }
        release {
            minifyEnabled true
            debuggable false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

dependencies {
    implementation project(path: ':data')

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.5.21"
    implementation 'androidx.core:core-ktx:1.7.0-alpha01'

    // Lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'

    // Worker Manager
    implementation "androidx.work:work-runtime-ktx:2.7.0-alpha05"

    // Google
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "com.google.android.material:material:1.4.0"
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation "androidx.fragment:fragment-ktx:1.3.6"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"

    // Firebase
    implementation 'com.google.firebase:firebase-core:19.0.0'
    implementation 'com.google.firebase:firebase-analytics:19.0.0'
    implementation 'com.google.firebase:firebase-crashlytics:18.2.0'
    implementation 'com.google.firebase:firebase-messaging:22.0.0'

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:3.0.1"

    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.5'

    // Date and Time
    implementation 'com.jakewharton.threetenabp:threetenabp:1.3.1'

    // Picasso
    implementation 'com.squareup.picasso:picasso:2.71828'

    // Animated Loading Button
    implementation 'com.github.razir.progressbutton:progressbutton:2.1.0'

    // OAuth
    implementation 'net.openid:appauth:0.9.1'

    // Koin
    implementation "org.koin:koin-core:2.0.1"
    implementation "org.koin:koin-androidx-viewmodel:2.0.1"

    // MarkdownView
    implementation 'io.noties.markwon:core:4.6.2'

    // SharedPrefsData
    implementation 'com.chibatching.kotpref:kotpref:2.13.1'
    implementation 'com.chibatching.kotpref:gson-support:2.13.1'
    implementation 'com.chibatching.kotpref:livedata-support:2.13.1'

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Data Binding
    implementation 'androidx.databinding:databinding-common:7.1.0-alpha05'
    implementation 'androidx.databinding:databinding-adapters:7.1.0-alpha05'

    // Unit Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'io.mockk:mockk:1.12.0'

    // Android Testing
    androidTestImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.benchmark:benchmark-junit4:1.0.0'

    // FastTextView
    implementation 'com.facebook.fbui.textlayoutbuilder:textlayoutbuilder:1.6.0'

    // Expandable View
    implementation 'net.cachapa.expandablelayout:expandablelayout:2.9.2'


}

apply plugin: 'com.google.gms.google-services'
